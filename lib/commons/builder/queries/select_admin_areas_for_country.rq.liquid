SELECT DISTINCT ?primarySort ?adminArea ?adminAreaType {
  {
    VALUES (?adminArea ?primarySort ?adminAreaType) { (wd:{{ country }} 1 wd:Q6256) }
  } UNION {
    # Find FLACSen of this country
    ?adminArea wdt:P17 wd:{{ country }} ;
          wdt:P31/wdt:P279* wd:Q10864048
    VALUES (?primarySort ?adminAreaType) { (2 wd:Q10864048) }
  } UNION {
    # Find cities with populations of over 250k
    ?adminArea wdt:P17 wd:{{ country }} ;
       wdt:P31/wdt:P279* wd:Q515 ;
       wdt:P1082 ?population .
    FILTER (?population > 250000)
    # Make sure the city is not also a FLACS
    MINUS { ?adminArea wdt:P31/wdt:P279* wd:Q10864048 }
    VALUES (?primarySort ?adminAreaType) { (3 wd:Q515) }
  }
} ORDER BY ?primarySort
