SELECT DISTINCT ?primarySort ?adminArea ?adminAreaType {
  {
    VALUES (?adminArea ?primarySort ?adminAreaType) { (wd:{{ config.country_wikidata_id }} 1 wd:Q6256) }
  } UNION {
    # Find FLACSen of this country
    ?adminArea wdt:P17 wd:{{ config.country_wikidata_id }} ;
          wdt:P31/wdt:P279* wd:Q10864048
    VALUES (?primarySort ?adminAreaType) { (2 wd:Q10864048) }
  } UNION {
    # Find cities or municipalities with populations of over 250k
    VALUES ?adminAreaType { wd:Q515 wd:Q15284 }
    ?adminArea wdt:P17 wd:{{ config.country_wikidata_id }} ;
       wdt:P31/wdt:P279* ?adminAreaType ;
       wdt:P1082 ?population .
    FILTER (?population > 250000)
    # Make sure the city is not also a FLACS
    MINUS { ?adminArea wdt:P31/wdt:P279* wd:Q10864048 }
    VALUES (?primarySort) { (3) }
  }{% if config.additional_admin_area_ids %} UNION {
    VALUES (?adminArea ?primarySort ?adminAreaType) {
{%- for admin_area_id in config.additional_admin_area_ids %}
      (wd:{{ admin_area_id }} 4 wd:Q24238356)
{%- endfor %}
    }
  }{% endif %}
} ORDER BY ?primarySort ?adminAreaType
